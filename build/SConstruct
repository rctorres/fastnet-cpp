#This is a scons MakeFile.
#Compiles and installs the FastNet package.

#Author: Rodrigo Coura Torres (Rodrigo.Torres@cern.ch)

from __future__ import print_function
from __future__ import unicode_literals
from __future__ import division
from __future__ import absolute_import

import os
import platform
import re
import fnmatch
import json
import stat
import sys

import sc_libs
import sc_matlab


#The matlab builder (calling mex) - ####### I NEED TO TURN THIS INTO A CYTHON COMPILET #######
def cythonBuild(target, source, env, for_signature):
	incPaths = ' '.join(['-I%s' % i for i in env['CPPPATH']])
	libPaths = ' '.join(['-L%s' % i for i in env['LIBPATH']])
	libList = ' '.join(['-l%s' % i for i in env['LIBS']])
	compFlags = ' '.join(env['CCFLAGS'])
	return '%s -cxx %s %s %s %s -o %s %s' % (env['MEX'], compFlags, incPaths, libPaths, libList, target[0], source[0]);


#loading the configuration file produced after calling "configure" script
config = None
try:
  with open('config_values.json', 'r') as f:
    config = json.loads(f.read())
except BaseException as e:
  print ('Error loading configuration file. Did you run "configure" script first?')
  sys.exit(1)


#Compiling flags.
globalCPPFlags = ['-DNO_OMP', '-DBOOST_ALL_DYN_LINK']
libCPPFlags = []
mexCPPFlags = []

#Getting whether to compile in debug mode.
debug = int(ARGUMENTS.get('debug', 0))
if debug > 0: globalCPPFlags += ['-DDEBUG=%d' % debug, '-g']

######## SHPULD BOOST REFERENCES GO TO CONFIGURE???? ############
incPath = ['../', os.path.join(os.environ['BOOST_HOME'], 'include')]
libPath = ['./', os.path.join(os.environ['BOOST_HOME'], 'lib')]

#Am I using a MAC computer? Then I apply some optimizations for it
if platform.system() == 'Darwin':
  libCPPFlags += ['-std=c++11']
elif platform.system() == 'Linux':
  incPath.append('/usr/include')
  libPath.append('/usr/lib')
  if debug == 0:
    libCPPFlags += ['-O3', '-m64']
    dist, version, branch = platform.dist()
    if (dist == 'debian'): libCPPFlags += ['-fopenmp']

#Creating our building environment.
env = Environment(CXX = 'g++', CPPPATH = incPath, ENV = os.environ) #This is for default C++ code
env.Append(BUILDERS = {'Cython' : Builder(generator = cythonBuild)}) #This is for dealing with cython compilations

######IF I NEED TO PASS ANY SPECIFIC VALUE TO THE CYTHON COMPILER, I CAN APPEND THESE VARIABLES TO THE ENVIRONMENT LIKE I'M DOING IN THE COMMAND BELLOW#####
#env.Append(MEX = config['matlab']['mex']) #Passing the path to the mex compiler.


### Creating the fastnet dynamic libraries.
libInstList = [];
for lib, opt in sc_libs.libs.iteritems():
  libName = env.SharedLibrary(target = lib,
                              source = Glob('../src/%s/*.c*' % lib),
                              CCFLAGS = globalCPPFlags + libCPPFlags,
                              LIBS = opt['LIBS'],
                              LIBPATH = libPath)
  libInstList.append(libName)


### Creating Python bindings. Starting it with the scripts which do not need compilation
scriptInstList = Glob('../script/python/*.py')

#Going over scripts bind to C++ code (using Cython for that)
#for mat, opt in sc_matlab.matlab.iteritems():
#  matBinding = env.Cython(target = mat,
#                          source = '../src/matlab/%s.cxx' % mat, 
#                          CCFLAGS = globalCPPFlags + mexCPPFlags, 
#                          LIBS = opt['LIBS'], 
#                          LIBPATH = libPath)
#  scriptInstList.append(matBinding);

#Processing bin files. Any executable that beed to be installed should go in this list.
binInstList = []


###Specifying the installations directories.
libInstDir = config['installation']['libDir']
scriptInstDir = config['installation']['scriptDir']
binInstDir = config['installation']['binDir']

#Associating the files list to their installation directories.
env.Install(libInstDir, libInstList)
env.Install(scriptInstDir, scriptInstList)
env.Install(binInstDir, binInstList)

#Creating the installation aliases.
instAll = env.Alias('install', [libInstDir, scriptInstDir, binInstDir])
env.Alias('install', [instAll])
